task compileInstaller(type: Exec)
task createInstaller(type: Copy)
task getInstallerDependencies(type: Exec)
task installComponentMavenArtifacts(type: Exec)
task packageInstaller(type: Zip)

defaultTasks "packageInstaller"

Date now = new Date()

compileInstaller {
	dependsOn getInstallerDependencies

	commandLine _getCommand("gox -osarch=\"darwin/amd64 linux/amd64 windows/amd64\" -output \"${buildDir}/talend-installer/{{.Dir}}-{{.OS}}-{{.Arch}}\" talend-installer/go")
	workingDir 'go'
}

createInstaller {
	dependsOn compileInstaller

	from new File(projectDir, "../tos-dependencies")
	into new File(buildDir, "talend-installer")
}

getInstallerDependencies {
	dependsOn installComponentMavenArtifacts

	commandLine _getCommand("go mod tidy")
	workingDir 'go'
}

installComponentMavenArtifacts {
	commandLine _getCommand("mvn clean install -f ./../pom.xml")
}

packageInstaller {
	dependsOn createInstaller

	archiveName = "tLiferayComponents-TOS-7.1.1-${now.format('yyyyMMddHHmmss')}.zip"
	destinationDir = file("$buildDir/dist")

	from "$buildDir/talend-installer"
}

private List<String> _getCommand(String command) {
	if (File.pathSeparator.equals(";")) {
		return ["cmd.exe", "/c", "${command}"]
	}

	return ["/bin/sh", "-c", "${command}"]
}