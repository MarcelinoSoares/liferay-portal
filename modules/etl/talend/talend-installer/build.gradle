import com.liferay.gradle.plugins.go.internal.util.OSDetector
import com.liferay.gradle.plugins.go.tasks.ExecuteGoTask

apply plugin: "com.liferay.go.defaults"

task compileInstaller(type: ExecuteGoTask)
task createInstaller(type: Copy)
task getInstallerDependencies(type: ExecuteGoTask)
task installComponentMavenArtifacts(type: Exec)
task installGox(type: ExecuteGoTask)
task packageInstaller(type: Zip)

defaultTasks "packageInstaller"

Date now = new Date()

compileInstaller {
	dependsOn getInstallerDependencies
	dependsOn installGox

	args "-osarch=darwin/amd64 linux/amd64 windows/amd64", "-output", "${buildDir}/talend-installer/installer-{{.OS}}-{{.Arch}}", "talend-installer/go"
	command "gox"
}

createInstaller {
	dependsOn compileInstaller

	from new File(projectDir, "../tos-dependencies")
	into new File(buildDir, "talend-installer")
}

getInstallerDependencies {
	dependsOn installComponentMavenArtifacts

	args "mod", "tidy"
}

installComponentMavenArtifacts {
	commandLine OSDetector.getCLI(), "/c", "mvn clean install -f ./../pom.xml"
}

installGox {
	args "get", "github.com/mitchellh/gox"
}

packageInstaller {
	dependsOn createInstaller

	archiveName = "tLiferayComponents-TOS-7.1.1-${now.format('yyyyMMddHHmmss')}.zip"
	destinationDir = file("$buildDir/dist")

	from "$buildDir/talend-installer"
}